#ifndef SHADOWS
#define SHADOWS

#ifdef _RECEIVE_SHADOWS
layout(std140) struct ShadowData
{
    mat4x4 LightViewProjMatrix;
};

layout(std140) uniform Shadows
{
    ShadowData _DirLightShadow;
    ShadowData _SpotLightShadows[MAX_SPOT_LIGHT_SOURCES];
};

uniform sampler2D _DirLightShadowMap;
uniform sampler2DArray _SpotLightShadowMapArray;

bool isFragVisible(float fragZ)
{
    return fragZ >= 0 && fragZ <= 1;
}

float getShadowTerm(float fragZ, float shadowMapDepth, float lightAngleCos)
{
    float bias = max(0.005 * (1 - lightAngleCos), 0.0005);
    return step(fragZ - bias, shadowMapDepth);
}
#endif

float getDirLightShadowTerm(vec3 posWS, float lightAngleCos)
{
    #ifdef _RECEIVE_SHADOWS
    vec4 shadowCoord = _DirLightShadow.LightViewProjMatrix * vec4(posWS, 1);
    float depth = texture(_DirLightShadowMap, shadowCoord.xy).x;
    return isFragVisible(shadowCoord.z) ? getShadowTerm(shadowCoord.z, depth, lightAngleCos) : 1;
    #else
    return 1;
    #endif
}

float getSpotLightShadowTerm(int index, vec3 posWS, float lightAngleCos)
{
    #ifdef _RECEIVE_SHADOWS
    vec4 shadowCoord = _SpotLightShadows[index].LightViewProjMatrix * vec4(posWS, 1);
    shadowCoord = shadowCoord / shadowCoord.w;
    float depth = texture(_SpotLightShadowMapArray, vec3(shadowCoord.xy, index)).x;
    return isFragVisible(shadowCoord.z) ? getShadowTerm(shadowCoord.z, depth, lightAngleCos) : 1;
    #else
    return 1;
    #endif
}

#endif